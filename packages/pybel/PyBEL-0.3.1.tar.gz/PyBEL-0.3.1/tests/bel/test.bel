##################################################################################
# Document Properties Section

SET DOCUMENT Name = "BEL Framework Small Corpus Document"
SET DOCUMENT Description = "Approximately 2000 hand curated statements drawn from 57 PubMeds."
SET DOCUMENT Version = "1.6"
SET DOCUMENT Copyright = "Copyright (c) 2011-2012, Selventa. All Rights Reserved."
SET DOCUMENT Authors = "Selventa"
SET DOCUMENT Licenses = "Creative Commons Attribution-Non-Commercial-ShareAlike 3.0 Unported License"
SET DOCUMENT ContactInfo = "support@belframework.org"

##################################################################################
# Definitions Section

DEFINE NAMESPACE MGI AS URL "http://resource.belframework.org/belframework/1.0/namespace/mgi-approved-symbols.belns"
DEFINE NAMESPACE CHEBI AS URL "http://resource.belframework.org/belframework/1.0/namespace/chebi-names.belns"
DEFINE NAMESPACE NCR AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-rat-complexes.belns"
DEFINE NAMESPACE HGNC AS URL "http://resource.belframework.org/belframework/1.0/namespace/hgnc-approved-symbols.belns"
DEFINE NAMESPACE SPAC AS URL "http://resource.belframework.org/belframework/1.0/namespace/swissprot-accession-numbers.belns"
DEFINE NAMESPACE PFH AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-human-protein-families.belns"
DEFINE NAMESPACE CHEBIID AS URL "http://resource.belframework.org/belframework/1.0/namespace/chebi-ids.belns"
DEFINE NAMESPACE MESHD AS URL "http://resource.belframework.org/belframework/1.0/namespace/mesh-diseases.belns"
DEFINE NAMESPACE GO AS URL "http://resource.belframework.org/belframework/1.0/namespace/go-biological-processes-names.belns"
DEFINE NAMESPACE SCHEM AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-legacy-chemical-names.belns"

DEFINE ANNOTATION CellStructure AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-cell-structure.belanno"
DEFINE ANNOTATION CellLine AS  URL "http://resource.belframework.org/belframework/1.0/annotation/atcc-cell-line.belanno"
DEFINE ANNOTATION TextLocation AS  LIST {"Abstract","Results","Legend","Review"}
DEFINE ANNOTATION CardiovascularSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-cardiovascular-system.belanno"
DEFINE ANNOTATION MuskuloskeletalSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-musculoskeletal-system.belanno"
DEFINE ANNOTATION Tissue AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-tissue.belanno"
DEFINE ANNOTATION Cell AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-cell.belanno"
DEFINE ANNOTATION Disease AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-disease.belanno"
DEFINE ANNOTATION Species AS  URL "http://resource.belframework.org/belframework/1.0/annotation/species-taxonomy-id.belanno"


##################################################################################
# Statements Section

SET STATEMENT_GROUP = "Group 1"

SET Disease = "Atherosclerosis"
SET CardiovascularSystem = "Arteries"
SET TextLocation = "Review"
SET Evidence = "Oxidation and nitration of macromolecules, such as proteins, DNA and lipids, are prominent
in atherosclerotic arteries."
SET Citation = {"PubMed","Trends in molecular medicine","12928037","","de Nigris F|Lerman A|Ignarro LJ|Williams-Ignarro S|Sica V|Baker AH|Lerman LO|Geng YJ|Napoli C",""}

pathology(MESHD:Atherosclerosis) positiveCorrelation biologicalProcess(GO:"lipid oxidation")
pathology(MESHD:Atherosclerosis) positiveCorrelation biologicalProcess(GO:"protein oxidation")

SET Evidence = "Arterial cells are highly susceptible to oxidative stress, which can induce both necrosis
and apoptosis (programmed cell death) [1,2]"

biologicalProcess(GO:"response to oxidative stress") increases biologicalProcess(GO:"apoptotic process")
biologicalProcess(GO:"response to oxidative stress") increases biologicalProcess(GO:necrosis)

SET Cell = "Endothelial Cells"
SET Evidence = "One such risk factor is oxLDL, which exert pro-apoptotic effects on a variety of cell
lineages, including endothelial cells and VSMCs, both in cultured cells and
in arteries [19–26]. Increased formation of oxygen radicals (and other radical
species) facilitates LDL oxidation and influences oxidation-sensitive mechanisms
[15]."

abundance(SCHEM:"Oxidized Low Density Lipoprotein") increases biologicalProcess(GO:"apoptotic process")

SET MuskuloskeletalSystem = "Muscle, Smooth, Vascular"

abundance(SCHEM:"Oxidized Low Density Lipoprotein") increases biologicalProcess(GO:"apoptotic process")
abundance(CHEBI:"oxygen radical") increases abundance(SCHEM:"Oxidized Low Density Lipoprotein")

SET Evidence = "Not surprisingly, high concentrations of exogenous reactive oxygen species (ROS),
generated by enzymatic reactions or by radiation, also stimulate the apoptosis
of endothelial cells in vitro [29–31]."

abundance(CHEBI:"reactive oxygen species") increases biologicalProcess(GO:"apoptotic process")

SET Evidence = "The peptide hormone angiotensin II and pro-inflammatory cytokines also trigger the
apoptotic program in endothelial cells [33,34]."

abundance(CHEBI:"angiotensin II") increases biologicalProcess(GO:"apoptotic process")

SET Evidence = "Aging, one of the major predictors for atherosclerotic lesion formation, increases
the sensitivity of endothelial cells to apoptosis induced by in vitro and in vivo
stimuli [35–37]."

biologicalProcess(GO:aging) positiveCorrelation pathology(MESHD:"Plaque, Atherosclerotic")
biologicalProcess(GO:aging) increases biologicalProcess(GO:"apoptotic process")

SET Evidence = "Shear stress enhances expression of the gene encoding the endothelial nitric oxide
synthase (eNOS) and further stimulates its enzymatic activity, leading to physiologic
low concentrations of nitric oxide (NO) within endothelial cells [44–46].
This continuous generation of NO prevents the apoptosis of endothelial cells,
thereby protecting the endothelial monolayer from injury [47,48]."

biologicalProcess(GO:"response to fluid shear stress") increases rnaAbundance(HGNC:NOS3)
biologicalProcess(GO:"response to fluid shear stress") increases catalyticActivity(proteinAbundance(HGNC:NOS3))
catalyticActivity(proteinAbundance(HGNC:NOS3)) directlyIncreases abundance(CHEBI:"nitric oxide")
abundance(CHEBI:"nitric oxide") decreases biologicalProcess(GO:"apoptotic process")

SET Evidence = "More importantly, intervention with L-arginine has induced beneficial effects on atherosclerosis
in experimental and human studies [46,49]."

abundance(CHEBI:"L-arginine") decreases pathology(MESHD:Atherosclerosis)

SET Tissue = "Endothelium, Vascular"
SET Evidence = "OxLDL induces the generation of ROS in different vascular cell types [15,58], and
might also promote a rise in ROS levels through a decrease in ROS degradation,
because oxLDL induces a reduction in the antioxidant enzymes Cu/Zn superoxide dismutase
[59] and glutathione peroxidase [60]."

abundance(SCHEM:"Oxidized Low Density Lipoprotein") increases abundance(CHEBI:"reactive oxygen species")
abundance(SCHEM:"Oxidized Low Density Lipoprotein") decreases proteinAbundance(HGNC:SOD1)
abundance(SCHEM:"Oxidized Low Density Lipoprotein") decreases proteinAbundance(PFH:"GPX Family")

SET CellLine = "U-937"
SET Evidence = "For instance, during 7-ketocholesterol-induced apoptosis of U937 cells, the cellular
antioxidant content falls rapidly [64], but administration of two potent antioxidants,
the aminothiols glutathione and N-acetylcysteine, can protect themonocytic
cells from 7-ketocholesterol-induced apoptotic cell death [64]."

abundance(SCHEM:"7-Ketocholesterol") increases biologicalProcess(GO:"apoptotic process")
abundance(SCHEM:"7-Ketocholesterol") negativeCorrelation abundance(CHEBI:antioxidant)
abundance(CHEBI:antioxidant) decreases (abundance(SCHEM:"7-Ketocholesterol") increases biologicalProcess(GO:"apoptotic process"))

SET Tissue = "Muscle, Smooth, Vascular"
SET Evidence = "In addition, another oxygen-radical scavenger enzyme, catalase, can prevent VSMC apoptosis
triggered by hydrogen peroxide [27,65]."

abundance(CHEBI:"hydrogen peroxide") increases biologicalProcess(GO:"apoptotic process")
proteinAbundance(HGNC:CAT) decreases (abundance(CHEBI:"hydrogen peroxide") increases biologicalProcess(GO:"apoptotic process"))

SET Evidence = "The proto-oncogene c-myc can promote cell death (probably mediated through p53) or
cell proliferation, depending on its expression level. It functions as a nuclear
phosphoprotein with particular properties of a transcription factor [68,69].
In serum deprived cultures, cells overexpressing c-myc readily undergo apoptosis.
In addition, deregulation of c-myc causes apoptosis of VSMCs deprived of growth
factors or treated with cytokines such as interferon-g [70]."

proteinAbundance(HGNC:MYC) increases biologicalProcess(GO:"apoptotic process")
proteinAbundance(HGNC:MYC) increases biologicalProcess(GO:"cell proliferation")

SET Evidence = "Tissue factor (TF) is a 47 kDa transmembrane glycoprotein that initiates blood coagulation
by binding coagulation factor VII and its activated form (factor VIIa),
to produce a high-affinity complex [72] that proteolytically activates factors
IX and X, leading to thrombin generation."

peptidaseActivity(complexAbundance(proteinAbundance(HGNC:F3),proteinAbundance(HGNC:F7))) directlyIncreases peptidaseActivity(proteinAbundance(HGNC:F9))
peptidaseActivity(complexAbundance(proteinAbundance(HGNC:F3),proteinAbundance(HGNC:F7))) directlyIncreases peptidaseActivity(proteinAbundance(HGNC:F10))
peptidaseActivity(proteinAbundance(HGNC:F9)) increases peptidaseActivity(proteinAbundance(HGNC:F2))
peptidaseActivity(proteinAbundance(HGNC:F10)) increases peptidaseActivity(proteinAbundance(HGNC:F2))

SET CellStructure = "Cell Membrane"
SET Evidence = "TF works at the surface of cell membranes, and its activity is highly dependent on
the presence of phosphatidylserine (PS), an anionic phospholipid that is redistributed
on the cell surface during apoptotic death and confers a potent pro-coagulant
activity to the apoptotic cell [74]."

abundance(CHEBI:"phosphatidyl-L-serine") increases peptidaseActivity(proteinAbundance(HGNC:F3))
biologicalProcess(GO:"apoptotic process") increases abundance(CHEBI:"phosphatidyl-L-serine")
abundance(CHEBI:"phosphatidyl-L-serine") increases biologicalProcess(GO:coagulation)

SET Evidence = "TIMP-3 can be anti-angiogenic by direct binding to vascular endothelial growth factor
(VEGF) receptor 2 in a matrix-metalloproteinase- independent manner [94]."

proteinAbundance(HGNC:TIMP3) decreases biologicalProcess(GO:angiogenesis)
complexAbundance(proteinAbundance(HGNC:TIMP3),proteinAbundance(HGNC:KDR)) decreases biologicalProcess(GO:angiogenesis)

UNSET STATEMENT_GROUP